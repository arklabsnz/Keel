buildscript {
    apply from: '../dependencies.gradle'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath Dependencies.kotlinGradlePlugin
        classpath Dependencies.androidMavenPublish
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

apply from: '../dependencies.gradle'

android {
    compileSdkVersion Versions.compileSdk
    namespace = 'nz.arklabs.keel.viewmodel'

    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.compileSdk
        aarMetadata {
            minCompileSdk = 29
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = rootProject.ext.group
                artifactId = 'viewmodel'
                version = rootProject.ext.version
            }
        }
    }
}

repositories {
    google()
    mavenCentral()
}

//gw dependencyUpdates -Drevision=release
dependencies {
    api Dependencies.lifecycleKtx
    api Dependencies.aacLifecycle
    api Dependencies.aacReactiveStreams
    implementation Dependencies.rxAndroid
    implementation Dependencies.rxRelay
    implementation Dependencies.rxJava
    api Dependencies.coroutinesCore

    implementation Dependencies.appCompat
    testImplementation Dependencies.junit
    implementation Dependencies.kotlinStdlib
}
